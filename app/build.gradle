apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android.targetSdkVerion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        testInstrumentationRunner rootProject.ext.android.testInstrumentationRunner
    }

    signingConfigs {
        debug {
            storeFile file(debugKey)
            // No debug config
        }

        release {
            storeFile file(releaseKey)
            storePassword KEY_PWD
            keyAlias ALIAS_NAME
            keyPassword ALIAS_PWD
        }
    }

    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"

            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }

        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"

            minifyEnabled true
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    // 输出apk名称为五子棋_v1.0_2015-01-15_wandoujia.apk
                    def fileName = "益智游戏集_v${defaultConfig.versionName}_${releaseTime()}.apk"
                    outputFileName = fileName
                }

            }
        }

        lintOptions { abortOnError false }
    }

}

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    if (rootProject.ext.isModule) {
        implementation project(":commonlib")
        implementation project(":modulemain")
        implementation project(":modulegomoku")
    }
}
